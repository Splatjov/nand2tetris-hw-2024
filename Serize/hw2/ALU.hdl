// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // zero the x input?
        Not16(in= x, out= notX);
        And16(a= x, b= notX, out= zeroX);
        Mux16(a= x, b= zeroX, sel= zx, out= x1);


        // negate the x input?
        Not16(in= x1, out= notX1);
        Mux16(a= x1, b= notX1, sel= nx, out= x2);


        // zero the y input?
        Not16(in= y, out= notY);
        And16(a= y, b= notY, out= zeroY);
        Mux16(a= y, b= zeroY, sel= zy, out= y1);


        // negate the y input?
        Not16(in= y1, out= notY1);
        Mux16(a= y1, b= notY1, sel= ny, out= y2);


        // compute (out = x + y) or (out = x & y)?
        Add16(a = x2, b = y2, out = addResult);
        And16(a= x2, b= y2, out= andResult);
        Mux16(a= andResult, b= addResult, sel= f, out= operationOut);
        

        // negate the out output?
        Not16(in= operationOut, out= notOperationOut);
        Mux16(a= operationOut, b= notOperationOut, sel= no, out= out, out[0..7] = result1, out[8..15] = result2, out[15] = out15);
        
        // if (out == 0) equals 1, else 0
        Or8Way(in= result1, out = firstHalf);
        Or8Way(in= result2, out = secondHalf);
        Or(a= firstHalf, b= secondHalf, out= zrResult);
        Not(in= zrResult, out= zr);
        
        // if (out < 0)  equals 1, else 0
        And(a= true, b= out15, out= ng);
        
}