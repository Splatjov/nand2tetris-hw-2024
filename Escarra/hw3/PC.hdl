// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];

    PARTS:
    Inc16(in= prevOut, out= prevOutPlus1);
    Mux16(a= prevOut, b= prevOutPlus1, sel= inc, out= prevOutAfterInc);
    Mux16(a= prevOutAfterInc, b= in, sel= load, out= prevOutAfterIncAfterLoad);
    Mux16(a= prevOutAfterIncAfterLoad, b[0..15]= false, sel= reset, out= prevOutAfterIncAfterLoadAfterReset);
    Register(in= prevOutAfterIncAfterLoadAfterReset, load= true, out= out, out=prevOut);
}