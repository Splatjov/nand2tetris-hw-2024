// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM64.hdl
/**
 * Memory of sixty four 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:

    DMux8Way(
        in=load,
        sel=address[0..2],
        a=LOADa,
        b=LOADb,
        c=LOADc,
        d=LOADd,
        e=LOADe,
        f=LOADf,
        g=LOADg,
        h=LOADh
    );

    RAM8(in=in, load=LOADa, address=address[3..5], out=OUTa);
    RAM8(in=in, load=LOADb, address=address[3..5], out=OUTb);
    RAM8(in=in, load=LOADc, address=address[3..5], out=OUTc);
    RAM8(in=in, load=LOADd, address=address[3..5], out=OUTd);
    RAM8(in=in, load=LOADe, address=address[3..5], out=OUTe);
    RAM8(in=in, load=LOADf, address=address[3..5], out=OUTf);
    RAM8(in=in, load=LOADg, address=address[3..5], out=OUTg);
    RAM8(in=in, load=LOADh, address=address[3..5], out=OUTh);

    Mux8Way16(
        a=OUTa,
        b=OUTb,
        c=OUTc,
        d=OUTd,
        e=OUTe,
        f=OUTf,
        g=OUTg,
        h=OUTh,
        sel=address[0..2],
        out=out
    );
}