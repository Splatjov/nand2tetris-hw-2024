// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
      Mux16(a = x, b = false, sel = zx, out = xMux);
      Not16(in = xMux, out = nXmux);
      Mux16(a = xMux, b = nXmux, sel = nx, out = xnMux);
      Mux16(a = y, sel = zy, out = yMux);
      Not16(in = yMux, out = nYmux);
      Mux16(a = yMux, b = nYmux, sel = ny, out = ynMux);
      Add16(a = xnMux, b = ynMux, out = xnynSum);
      And16(a = xnMux, b = ynMux, out = xnynAnd);
      Mux16(a = xnynAnd, b = xnynSum, sel = f, out = xnynAndSumMux);
      Not16(in = xnynAndSumMux, out = nXnYnAndSumMux);
      Mux16(a = xnynAndSumMux, b = nXnYnAndSumMux, sel = no, out = out, out[15] = ng, out[0..7] = left, out[8..15] = right);
      Or8Way(in = left, out = leftOrWay);
      Or8Way(in = right, out = rightOrWay);
      Or(a = leftOrWay, b = rightOrWay, out = zrn);
      Not(in = zrn, out = zr);
}