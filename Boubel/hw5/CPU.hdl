// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in = instruction[15], out = ninst1);
    Not(in = ninst1, out = ninst2);
    And(a = ninst2, b = instruction[5], out = instand5);
    And(a = instruction[4], b = ninst2, out = instand4);
    Or(a = ninst1, b = instand5, out = instor);
    Mux16(a = instruction, b = alu, sel = ninst2, out = instmuxalu);
    ARegister(in = instmuxalu, load = instor, out[0..14] = addressM, out = areg);
    Mux16(a = areg, b = inM, sel = instruction[12], out = aregmuxinm);
    DRegister(in = alu, load = instand4, out = dreg);
    ALU(x = dreg, y = aregmuxinm, zx = instruction[11], nx = instruction[10], zy = instruction[9],ny = instruction[8], f = instruction[7], no = instruction[6], out = alu, out = outM, zr = zralu, ng = ngalu);
    Not(in = zralu, out = notzralu);
    Not(in = ngalu, out = notngalu);
    And(a = notzralu, b = notngalu, out = nzrngaluand);
    And(a = instruction[0], b = nzrngaluand, out = nzrngaluand0);
    And(a = instruction[1], b = zralu, out = zraluand1);
    And(a = instruction[2], b = ngalu, out = ngaluand2);
    And(a = instruction[3], b = ninst2, out = writeM);
    Or(a = ngaluand2, b = zraluand1, out = zrngaluandor);
    Or(a = zrngaluandor, b = nzrngaluand0, out = nzrngor);
    And(a = nzrngor, b = ninst2, out = j);
    PC(in = areg, load = j, inc = true, reset = reset, out[0..14] = pc);
}