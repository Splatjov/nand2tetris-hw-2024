// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=xOrZeroOut);          
    Not16(in=xOrZeroOut, out=notx);                  
    Mux16(a=xOrZeroOut, b=notx, sel=nx, out=xOrZeroOrMux16Out);
    Mux16(a=y, b=false, sel=zy, out=yOrZeroOut);    
    Not16(in=yOrZeroOut, out=noty);                
    Mux16(a=yOrZeroOut, b=noty, sel=ny, out=yOrZeroOrMux16Out);
    Add16(a=xOrZeroOrMux16Out, b=yOrZeroOrMux16Out, out=xPlusY);
    And16(a=xOrZeroOrMux16Out, b=yOrZeroOrMux16Out, out=xAndY);   
    Mux16(a=xAndY, b=xPlusY, sel=f, out=xOpY);
    Not16(in=xOpY, out=xOpYNot);
    Mux16(a=xOpY, b=xOpYNot, sel=no, out[0..7]=outLSB, out[8..14]=outMSB, out[15]=outMSBbit);
    Mux16(a[0..7]=outLSB, a[8..14]=outMSB, a[15]=outMSBbit, b=true, sel=false, out=out);
    Or8Way(in=outLSB, out=xOpYOr1);
    Or8Way(in[0..6]=outMSB, in[7]=outMSBbit, out=xOpYOr2);
    Or(a=xOpYOr1, b=xOpYOr2, out=xOpYOr);
    Not(in=xOpYOr, out=zr);
    Or(a=outMSBbit, b=false, out=ng);
}