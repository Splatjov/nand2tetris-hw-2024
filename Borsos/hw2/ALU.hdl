// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a = x, b = false, sel = zx, out = xZero);

    Not16(in = xZero, out = notx);
    Mux16(a = xZero, b = notx, sel = nx, out = xDone);

    Mux16(a = y, b = false, sel = zy, out = yZero);

    Not16(in = yZero, out = noty);
    Mux16(a = yZero, b = noty, sel = ny, out = yDone);

    Add16(a = xDone, b = yDone, out = xAddy);
    And16(a = xDone, b = yDone, out = xAndy);
    Mux16(a = xAndy, b = xAddy, sel = f, out = out1);

    Not16(in = out1, out = notOut1);
    Mux16(a = out1, b = notOut1, sel = no, out = final);

    Mux16(a = final, b = false, sel = false, out[0..7] = low);
    Mux16(a = final, b = false, sel = false, out[8..15] = high);

    Or8Way(in = low, out = oneFinalLow);
    Or8Way(in = high, out = oneFinalHigh);

    Or(a = oneFinalLow, b = oneFinalHigh, out = orFinal);
    Not(in = orFinal, out = zr);

    And16(a = final, b = true, out[15] = ng, out[0..14] = drop);

    Or16(a = final, b = false, out = out);
}